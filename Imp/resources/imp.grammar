start = trim @(namespace, newlines, 0, 0) final_trim

snippet_entry = trim @(snippet_function, semicolon_or_newline, 1, 0) final_trim

none = /&*/
ws = /\s+/
trim = /\s*/
final_trim = /\s*$/
newlines = /(\s*\n)+\s*/
one_or_no_newline = /[ \t]*(\r\n)?[ \t]*/
comma = trim "," trim
spaces = /[ \t]+/
dot = "."
path_separator = "."
id = /[\$a-zA-Z0-9_]+/
comma_or_newline = /\s*((\s*\n)+|,)\s*/
semicolon_or_newline = /\s*((\s*\n)+|;)\s*/

string = ('"' /[^"]*/ '"') | ("'" /[^']*/ "'")
bool = "true" | "false"
int = /-?[0-9]+/
float = /-?([0-9]*\.)?[0-9]+f?/
operator = '+' | '-' | '/' | '*' | '<=' | '>=' | '<' | '>' | '==' | '!=' | '&&' | '||' | /\$\w+/
set_operator = '=' | '+=' | '-=' | '*=' | '/=' | '@='
value = string | bool | int | float
operation_separator = trim operator trim

arguments = "(" trim @(expression, comma_or_newline, 0, 0) trim ")"

optional_arguments = trim arguments

reference_token = id @(arguments, none, 0, 1)

optional_array = "[" trim expression trim "]"

id_with_optional_array = id @(optional_array, none, 0, 1)

reference = @(id_with_optional_array, path_separator, 1, 0) @(optional_arguments, none, 0, 1)

declare_symbol = id trim @(type_info, none, 0, 1)

type_info = ":" trim type

type = @(id, path_separator, 1, 0) @(optional_list_suffix, none, 0, 1)

optional_list_suffix = trim "[" trim "]"

statement =
	declare_variable
	| if
	| while
	| return
	| for
	| assignment
	| expression


expression_part =
	  instantiate
	| value
	| reference	
	| closed_expression

closed_expression = "(" trim expression ")"

expression = @(expression_part, operation_separator, 1, 0)

namespace = "module" ws id trim "{" trim @(class, newlines, 0, 0) trim "}"

function_definition = @(function_attribute, none, 0, 1) id trim "(" @(declare_symbol, comma, 0, 0) ")" trim @(type_info, none, 0, 1) trim long_block

snippet_function = @(function_attribute, none, 0, 1) id trim "(" @(declare_symbol, comma, 0, 0) ")" trim @(type_info, none, 0, 1) trim long_block_any

abstract_function = "abstract" ws id trim "(" @(declare_symbol, comma, 0, 0) ")" trim @(type_info, none, 0, 1)

function_attribute =  "private" ws

class_statements = abstract_function | function_definition | property_declaration

property_declaration = id trim type_info

class = "class" ws id trim @(optional_parent_classes, none, 0, 1) "{" trim @(class_statements, newlines, 0, 0) trim "}"

optional_parent_classes = ":" trim @(id, comma_or_newline, 1, 0) trim

block = long_block | short_block

short_block = one_or_no_newline statement

long_block = "{" trim @(statement, semicolon_or_newline, 0, 0) trim "}"

long_block_any = "{" trim any_statement trim "}"

any_statement = class | statements

statements = @(statement, semicolon_or_newline, 0, 0)

if = "if" trim "(" trim expression trim ")" trim block

while = "while" trim "(" trim expression trim ")" trim block

optional_expression = spaces expression

return = "return" @(optional_expression, none, 0, 1)

optional_assignment = trim "=" trim expression

declare_variable = "var" ws id trim @(type_info, none, 0, 1) @(optional_assignment, none, 0, 1)

assignment = reference trim set_operator trim expression

for = "for" trim "(" trim "var" ws id ws "in" ws reference trim ")" trim long_block

instantiate = "new" ws type trim arguments